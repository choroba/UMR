#!/usr/bin/perl
use warnings;
use strict;
use experimental qw( signatures );

use List::Util qw{ sum };
use Path::Tiny qw{ path };
use Text::Table;
use XML::XSH2 qw{ xsh };

sub size($name) {
    return 25 if $name =~ /pdtsc/;
    return 10 if $name =~ /wsj0072/;
    return  6 if $name =~ /wsj0013/;
    return 50 if $name =~ /sallust|latin/;
    return 1000
}

{   package XML::XSH2::Map;
    # No warnings 'once'.
    our ($size, $basename, $f);
}


my $pdtc2a = $ENV{UFAL_PDTC30} or die '$UFAL_PDTC30 not set';
my $umr    = $ENV{UFAL_UMR}    or die '$UFAL_UMR not set';
my $latin  = "$umr/data/latin/LDT_PML_tectogrammatical_130317"
           . '/LDT_Sallust/Sallust_all_files/';

for my $m (qw( data/PDT/pml/tamw/dtest/ln95046_093.m
               data/PDT/pml/tamw/dtest/ln94210_111.m
               data/PDTSC/pml/dtest/pdtsc_146_2.05.m
               data/PDTSC/pml/dtest/pdtsc_093_3.02.m
               data/PCEDT-cz/pml/dtest/wsj0013.cz.m
               data/PCEDT-cz/pml/dtest/wsj0072.cz.m
               sallust.afun.normalized.m
)) {
    my $f = path($pdtc2a, $m);
    $f = path($latin, $m) unless -f $f;
    $XML::XSH2::Map::f = $f->stringify;
    $XML::XSH2::Map::basename = my $basename = $f->basename;
    my $size = size($basename);
    $XML::XSH2::Map::size = $size;

    xsh <<~ '__XSH__';
        quiet ;
        register-namespace pml http://ufal.mff.cuni.cz/pdt/pml/ ;
        open $f ;
        my $c = count(//pml:s[count(preceding::pml:s) < $size]/pml:m) ;
        perl { $count{ substr $basename, 0, 1 }{m} += $c } ;
        my $cs = count(//pml:s[count(preceding::pml:s) < $size]) ;
        perl { $count{ substr $basename, 0, 1 }{s} += $cs } ;
    __XSH__

    $XML::XSH2::Map::f =~ s/\.m$/.t/;

    xsh <<~ '__XSH__';
        open $f ;
        my $c = count(//pml:trees/pml:*[count(preceding-sibling::pml:*) < $size]//pml:functor) ;
        perl { $count{ substr $basename, 0, 1 }{t} += $c } ;
    __XSH__
}

for my $u (qw( ln95046_093
               ln94210_111
               pdtsc_146_2.05
               pdtsc_093_3.02
               wsj0013.cz
               wsj0072.cz
               latin_conv_50_dmr
)) {
    for my $dir (
        path($umr, 'github', 'data', 'czech', 'PDT-C-dtest'),
        path($umr, 'github', 'data', 'latin', 'LDT-manual-comparison')
    ) {
        my ($f) = $dir->children(qr/^\Q$u.umr\E$|\Q$u\E-conv\.umr/);
        next unless $f;

        my $subcorpus = $f =~ /latin/ ? 's' : substr path($f)->basename, 0, 1;

        my $size = size($u);
        open my $pipe,
            '-|', qw{ btred -QTe writeln(scalar($this->descendants)) }, $f
            or die $!;
        chomp( my @out = <$pipe> );
        $XML::XSH2::Map::count{$subcorpus}{uc}
            += sum(@out[0 .. (@out > $size ? $size - 1 : $#out)]);
        my @files = glob "$dir/$u-*-all.umr";
        @files = glob "$dir/$u-??.umr" unless @files;
        @files = "$dir/FIXED_TOKENS_latin_umr-0001.txt" unless @files;
        for my $f (@files) {
            next unless -f $f;

            open my $pipe, '-|',
                qw{ btred -QTe writeln(scalar($this->descendants)) },
                $f or die $!;
            chomp( my @out = <$pipe> );
            $XML::XSH2::Map::count{$subcorpus}{um}
                += sum(@out[0 .. (@out > $size ? $size - 1 : $#out)]);
        }
    }
}

my %ROW = (PDT   => 'l',
           PDTSC => 'p',
           PCEDT => 'w',
           LDT   => 's');

my @COLS = qw( s m t um uc );

my $tt = 'Text::Table'->new('corpus', 'sentences', 'tokens', 'nodes',
            'UMR manual', 'UMR converted');

for my $col (@COLS) {
    $XML::XSH2::Map::count{total}{$col}
        = sum(map $XML::XSH2::Map::count{ $ROW{$_} }{$col} // 0,
              grep 'LDT' ne $_, keys %ROW);
}

for my $row (qw( PDT PDTSC PCEDT total LDT )) {
    $tt->add($row,
             map $_ // '-',
             @{ $XML::XSH2::Map::count{ $ROW{$row} // $row } }{@COLS});
}
print $tt;
