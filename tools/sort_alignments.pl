#!/usr/bin/env perl
# Reads UMR file generated by Federica's converter from UD. Sorts alignment
# lines by the order of the aligned words.
# Copyright © 2025 Dan Zeman <zeman@ufal.mff.cuni.cz>
# License: GNU GPL

use utf8;
use open ':utf8';
binmode(STDIN, ':utf8');
binmode(STDOUT, ':utf8');
binmode(STDERR, ':utf8');

my @alines = ();
while(<>)
{
    # Is this an alignment line?
    if(m/^(s[0-9]+[a-záčďéíňóřšťúýž][0-9]*): ([0-9]+)-.+$/)
    {
        my %aline =
        (
            'line' => $_,
            'variable' => $1,
            'tokindex' => $2
        );
        push(@alines, \%aline);
    }
    # Is this the first line after alignments?
    elsif(scalar(@alines) > 0)
    {
        # Sort and print the alignments.
        @alines = sort
        {
            my $r = $a->{tokindex} <=> $b->{tokindex};
            unless($r)
            {
                $r = $a->{variable} cmp $b->{variable};
            }
            $r
        }
        (@alines);
        foreach my $aline (@alines)
        {
            print($aline->{line});
        }
        @alines = ();
        print;
    }
    # Other lines can be directly passed through.
    else
    {
        print;
    }
}
